`shadow-cljs.edn` também precisa de uma sessão de `:builds`. Builds deve ser um mapa de builds *keyed* pelo ID da build: 

.Um arquivo de configuração com um mapa da build.
```
{:dependencies [[some-library "1.2.1"] ...]
 :source-paths ["src"]
 :builds
 {:app   {:target     :browser
          ... browser-specific options ...}
  :tests {:target :karma
          ... karma-specific options ...}}}
```

Cada build descreve artefatos que o compilador vai construir. O build target é uma funcionalidade extensível do `shadow-cljs`, e o compilador já vem com algumas delas. 

== Build Target

Cada build em `shadow-cljs` deve definir um `:target` qual define onde você pretende que seu código seja executado. Existem padrões built-ins para cada <target-browser,browser>> e <<target-node,`node.js`>>. Eles também compartilham os conceitos básicos de ter os modos `:dev` e `:release`. O modo `:dev` provê todas as facilidades do desenvolvimento como compilação rápida, live reloading e a REPL. O modo `:release` vai produzir um output otimizado para produção. 

Targets é coberto em um capítulo separado

Aqui tem alguns deles:

[Horizontal]
<<target-browser, `:browser`>>:: Código de output adequado para rodar no navegador.
<<target-bootstrap, `:bootstrap`>>:: Código de output adequado para rodar em um ambiente cljs.
<<target-browser-test, `:browser-test`>>:: Examina os arquivos de testes arquivos obrigatórios, e a saída de testes adequada para rodar no navegador.
<<target-karma, `:karma`>>:: Examina os arquivos de testes arquivos obrigatórios, e uma saída de testes compatível com o  karma-runner. Mais informações http://karma-runner.github.io/2.0/index.html[Karma].
<<target-node-library, `:node-library`>>:: Saída de código adequada para usar como uma biblioteca Node.
<<target-node-script, `:node-script`>>:: Saída de código adequada para usar como um script node.
<<target-npm-module, `:npm-module`>>:: Saída de código adequada para usar como um módulo npm.

Cada target é coberto com mais detalhes no próprio capitulo desde que as opções de build remanescentes varia de acordo com o target selecionado.

== Opções de desenvolvimento [[devtools]]

Cada build `:target` tipicamente provê algum suporte de desenvolvimento. Eles são agrupado debaixo da chave `:devtools` para cada `:build`.

=== REPL

Quando rodando o `watch` o código para a REPL é injetado automáticamente e geralmente não requer configuração adicional. Opções adicionais são disponíveis para controlar o comportamento da REPL:

- `:repl-init-ns` permite configurar em qual namespace a REPL vai iniciar. O padrão é `cljs.user`.
- `:repl-pprint` faz a REPL usar `cljs.pprint` ao invés do habitual `pr-str` quando exibindo os resultados avaliados. Padrão é false.
```
{...
 :builds
 {:app {...
        :devtools {:repl-init-ns my.app
                   :repl-pprint true
                   ...}}}}
```


=== Preloads

Como desenvolvedor a maior parte do seu tempo é gasto no modo de desenvolvimento. Você provavelmente está familiarizado com as ferramentas como `figwhell`, `boot-reload`, e `devtools`. É quase certo que você quer um ou mais desses no seu processo de build.

Preload são usados para forçar certos namespaces na frente do seu Javascript gerado. Isso é geralmente usado para injetar ferramentas e intermediar antes da aplicação carregar e rodar. A opção de preload é simples, uma lista de namespaces no `:devtools`/`:preloads` na seção do `shadow-cljs-edn`: 

```
{...
 :builds
 {:app {...
        :devtools {:preloads [fulcro.inspect.preload]
                   ...}}}}
```

NOTE: Desde a versão 2.0.130 shadow-cljs automáticamente adiciona `cljs-devtools` para preloads em
`watch` e `compile` se eles estão no classpath. Tudo que precisa fazer é ter certeza de ter `cljs-devtools` no
target especifico, você pode suprimir esse comportamento adicionando `:console-support false` para o seção `:devtools`
desses targets.

=== Hot Code Reload

Os ecosistemas React e o ClojureScript combinam para fazer esse tipo de coisa super útil. O sistema `shadow-cljs`
inclue tudo que você precisa para funcionar o hot reload do código, sem precisar recorrer a ferramentas externas.

Com a finalidade de usar isso simplesmente rode:

```
shadow-cljs watch build-id
```

=== Ciclo de vida dos Hooks

É possível configurar o compilador para rodar funções antes antes do hot reload do código se transformar em código atualizado, e depois também. Eles são úteis para parar/iniciar as coisas que caso contrários, se apróxima do código antigo.

Podem ser configuradas através da sessão `:devtools` no seu arquivo de configuraçnao ou diretamente no código através dos tags metadados.

==== Metadados

É possível configurar certos metadados em CLJS `defn` para informar ao compilador que aquelas funções deveriam ser chamadas em certo momento quando estiver em live reloading.

.configuração hook via metadata
```clojure
(ns my.app)

(defn ^:dev/before-load stop []
  (js/console.log "stop"))

(defn ^:dev/after-load start []
  (js/console.log "start"))
```

Isso chamaria `my.app/stop` antes de carregar qualquer código novo e `my.app/start` quando todo o novo código for carregado. É possível também tagear multiplas funções como essa e eles serão chamados em uma ordem de depêndencia o seus namespaces.

Existem também variantes assíncronas, nesse caso você precisa fazer algum trabalho assíncrono que deveria completar antes de processar com o processo de reload.

.exemplo de async hooks
```clojure
(ns my.app)

(defn ^:dev/before-load-async stop [done]
  (js/console.log "stop")
  (js/setTimeout
    (fn []
      (js/console.log "stop complete")
      (done)))

(defn ^:dev/after-load-async start [done]
  (js/console.log "start")
  (js/setTimeout
    (fn []
      (js/console.log "start complete")
      (done)))
```

IMPORTANTE: A função vai receber uma função callback que deve ser chamada quando esse trabalho for completado. Se a função callback não for chamada o processo não será processado.

É possível tagear os namespaces com metadados então eles não irão nunca ser carregados mesmo sendo compilados. 

.Um non-reloadable ns
```
(ns ^:dev/once my.thing)

(js/console.warn "will only execute once")
```

Namespaces podem ser tageados para sempre serem recarregados.
Namespaces can also be tagged to always reload.

.Um always-reloadable ns
```
(ns ^:dev/always my.thing)

(js/console.warn "will execute on every code change")
```


==== Configuração

Além do metada é possível configurar os hooks do clico de vida através do `shadow-cljs.edn`.

[Horizontal]
`:before-load`:: Um símbolo (com namespace) de uma função para rodar antes de atualizar 
os arquivos que precisam ser recompilados. Essa função *must* é síncrona em natureza.
`:before-load-async`:: Um símbolo (com namespace) de uma função `(fn[done])` para rodar depois que o hot reload estiver completo. Essa função consegue fazer processamento assíncrono, mas o *must* chama `(done)` para indicar isso está completo.
`:autoload`:: Um boolean controlando se o código deveria ser hot loaded. Implicitamente `true` se qualquer uma das duas callbacks é setada. Sempre habilitado para o alvo `:browser` por padrão, configure para `false` para desabilitar.
`:ignore-warnings`:: Um boolean controlando se o código com alertas deveria ser recarregado. Padrão é `false`.

.Um exemplo do ciclo de vida dos hooks.
```
{...
 :builds
 {:app {...
        :devtools {:before-load  my.app/stop
                   :after-load   my.app/start
                   ...}}}}
```

DICA: Se nem `:after-load` nem `:before-load` estão configurados, o compilador vai apenas se preocupar para recarregar o código no alvo `:browser`. Se você ainda quer recarregar mas não precisa de algum dos callback, você pode configurar `:autoload true`, ao invés.

== Build Hooks [[build-hooks]]

Algumas vezes desejamos executar código customizado em um estágio especifico na pipeline de compilação. `:build-hooks` permite declarar quais funções devem ser chamados e eles tem controle total ao estado de build nesse tempo. Isso é bastante poderoso e abre muitas possibilidades de opções de ferramentas.

Eles são configurados por build dentro da chave `:build-hooks`

.Exemplo :build-hooks
```clojure
{...
 :builds
 {:app {:target ...
        :build-hooks
        [(my.util/hook 1 2 3)]
        ...}}}}
```

.Exemplo de código hook
```clojure
(ns my.util)

(defn hook
  {:shadow.build/stage :flush}
  [build-state & args]
  (prn [:hello-world args])
  build-state)
```

Esse exemplo chamaria `(my.util/hook build-state 1 2 3)` antes da build completar o `:flush` <<compilation-stages, stage>> (ex.: escreva no disco). O exemplo deveria exibir `[:hello-world (1 2 3)]` mas por favor, faça algo mais últil nos hooks de verdade.

O hook é apenas uma função *Clojure* normal com alguns metadados adicionais. O metadado `{:shadow.build/stage :flush}` informa ao compilador para chamar esse hook para `:flush` apenas. Você deve ao invés configurar `{"shadow.build/stages #{:configure :flush}}` se o hook deve ser chamado depois de múltiplos estágios. Pelo menos um estagio configurado é obrigatório desde o hook de outra forma nunca faz nada.

Toda build de hooks será chamada depois do `:target` concluído. Ele vai recever o `build-state` (um mapa clojure como todos os dados de build atuais) como primeiro argumento e *must* retornar esse `build-state` modificado ou não modificado. Quando usando múltiplos estágios você pode adicionar dados para o `build-state` que outras etapas podem acessar. Isso é fortemenete aconselhavel usar chaves namespaced apenas para ter certeza de não quebrar a build acidentalmente.

O `build-state` tem algumas imporantes entidades das quais deve ser úteis para seus hooks:

- `:shadow.build/build-id` - o id do build atual (ex. `:app`)
- `:shadow.build/mode` - `:dev` ou `:release`
- `:shadow.build/stage` - a etapa atual
- `:shadow.build/config` - a build de configuração. É possível ao invés de salvar o dado de configuração para o hook em um build config diretamente ou passar ele como argumento para o hook em si

IMPORTANTE: Rodando `watch` todos os hooks serão chamados repetidamente para cada build. Evite fazer muito trabalho porque eles podem consideravelmente impactar a performance da build

=== Estapas de compilação [[compile-stages]]

As possíveis estapas de `:build-hooks` são:

- `:configure` - inicial `:target` configuração especifica
- `:compile-prepare` - chamado antes que qualquer compilação seja feita
- `:compile-finish` - chamado depois que toda compilação é feita
- `:optimize-prepare` - chamado antes de rodar a fase de otimização do Closure Compiler (`:release` only)
- `:optimize-finish` - chamado depois que o Closure é feito (`:release` only)
- `:flush` - chamado depois que tudo foi embutido no disco

Enquanto `watch` o `:configure` é chamado apenas uma vez. Qualquer dos outros deve ser chamado 
novamente (em rodem) para cada re-compilação. O `build-state` será re-usado até o build config mudar aquele pedaço e isso será jogado fora e um novo será criado.

== Cache do Compilador

`shadow-cljs` vai cachear todos os resultados da compilação por padrão. O cache é invalidade sempre que alguma coisa relevante para o arquivo fonte muda (ex.: modificar as configurações do compilador, mudar dependências, etc.). Isso melhora significativamente a experiência do desenvolvedor desde que a compilação incremental vai ser *muito* mais rápido que começar do zero.


Invalidando o cache contudo pode nem sem sempre ser feito confiável se você estiver usando um monte de macros com efeitos coloterais (lendo arquivos, salvando coisas fora do estado do compilador, etc.). Nesses casos vocie deve precisar desabilitar o cache por completo.

Namespaces que são conhecidos por incluir efeitos colaterais macros prodem ser bloqueados. Eles não serão cacheados por si só e os namespaces exigindo que eles não sejam cacheados também. A bibliotéca https://github.com/cerner/clara-rules[clara-rules] tem efeitos colaterais macros e é bloqueado por padrão. Você pode especificar quais namespaces bloquear globalmente através da configuração `:cache-blockers`. Ele espera um conjunto de simbolos de namespace.

.clara.rules exemplo de bloqueio de cache (feito por padrão)
```
{...
 :cache-blockers #{clara.rules}
 :builds {...}}
```

Além do mais é possível controlar quanto de cache é feito através do `:build-options` `:cache-level`. As opções suportadas são:

[horizontal]
`:all`:: O padrão, todos os arquivos CLJS são cacheados
`:jars`:: Apenas cacheia os arquivos das bibliotécas, arquivos fonte nos arquivos `.jar`
`:off`:: Não cacheia nenhum resultado de compilação do CLJS (a opção mais lenta)

.Compilação sem cache
```
{...
 :builds
 {:app
  {:target :browser
   ...
   :build-options
   {:cache-level :off}}}}
```

Os arquivos de cache são guardados em um diretório dedicado para cada build então o cache nunca é compartilhado entre o builds. Um build com o id `:app` possui o o diretorio de cache `:dev` em: 

.Cache location for `cljs/core.cljs`
```
target/shadow-cljs/builds/app/dev/ana/cljs/core.cljs.cache.transit.json
```

A configuração `:cache-root` padrão para `target/shadow-cljs` controla onde todo os arquivos de cache serão escritos. Só é possível ser configurado globalmente, não por build.

```
{:source-paths [...]
 :dependencies [...]
 :cache-root ".shadow-cljs"
 :builds ...}

;; cache vai para
;; .shadow-cljs/builds/app/dev/ana/cljs/core.cljs.cache.transit.json
```

O `:cache-root` é sempre resolvido relativo ao diretório do projeto. É possível especificar paths absolutos também (ex. `/tmp/shadow-cljs`).

== Closure Defines [[closure-defines]]

A Biblioteca & Compilador Closure permite você definir variáveis que são essencialmente constantes em tempo de compilação. Você pode usar isso para configurar determinadas funcionalidades da sua build. Desde de que o compilador Closure trata esses como constantes quando rodando otimizações `:advanced` que são inteiramente suportados no Dead-Code-Eliminitation e pode ser usado para remover certas partes de código que deveria não ser incluido no build de `release`.

Defina eles em seu código

```clojure
(ns your.app)

(goog-define VERBOSE false)

(when VERBOSE
  (println "Hello World"))
```

Isso define as variáveis `your.app/VERBOSE` como `false` por padrão. Isso vai causar o `println` para ser removido na compilação `:advanced`. Você pode esconder esse detalhe `true` através da opção `:closure-defines` da qual vai habilitar o `println`. Isso pode ser feito apenas para modo de desenvolvimento ou para sempre.

```clojure
{...
 :builds
 {:app
  {:target :browser
   ...
   :modules {:app {:entries [your.app]}}
   ;; to enable in development only
   :dev {:closure-defines {your.app/VERBOSE true}}
   ;; to enable always
   :closure-defines {your.app/VERBOSE true}
   ;; you may also enable it for release as well
   :release {:closure-defines {your.app/VERBOSE true}}
   }}
```

DICA: Geralmente é seguro usar a variante "disabled" como padrão desde que ela faz menos coisas prováveis de ter incluida no build de `release` quando eles não deveriam. Esquecendo de setar a variável `:closure-defines` deveria nem sempre resultar em menos código sendo não mais usado.

*Closure Defines a partir de uma Biblioteca Closure*

- `goog.DEBUG`: A biblioteca Closure usa isso para várias funcionalidades de desenvolvimento. `shadow-cljs` automaticamente configura ela para `false` para builds de `release`.
- `goog.LOCALE` Pode ser usado para configurar certas funcionalidades de localização como `goog.i18n.DateTimeFormat`. Aceita uma string local sendo o valor padrão `en`. Praticamente todas as locales são suportadas, veja https://github.com/google/closure-library/blob/master/closure/goog/i18n/datetimesymbols.js[aqui] e https://github.com/google/closure-library/blob/master/closure/goog/i18n/datetimesymbolsext.js[aqui].

== Compiler Options [[compiler-options]]

O compilador CLJS suporta varias opções que influência como algum código é gerado. Para a maior parte do `shadow-cljs` vai pegar alguns bons padrões para cada `:target` mas você deve ocasionalmente querer mudar alguns deles.

Eles são todos agrupados dentro da chave `:compiler-options` no seu build de configuração.

```clojure
{:dependencies [...]
 :builds
 {:app
  {:target :browser
   ...
   :compiler-options {:fn-invoke-direct true}}}}
```

Muito dos padrões do ClojureScript https://clojurescript.org/reference/compiler-options[Compiler Options] são habilitados por padrão ou não se aplica. Então 
pouco deles na verdade tem algum efeito. Um monte deles são especificos para certo tipo de `:target` e não aplicado universalmente (ex.: `:compiler-options {:output-wrapper true}` é apenas relevante para `:target :browser`).

A opções atualmente suportadas são:

- `:optimizations` suporta `:advanced`, `:simple` ou `:whitespace`, padrão para `:advanced`. `:none` é o padrão para desenvolvimento e não pode ser configurado manualmente.
- `:infer-externs` `:all`, `:auto`, `true` ou `false`, padrão para `true`
- `:static-fns` (Boolean) padrão para `true`
- `:fn-invoke-direct` (Boolean) padrão para `false`
- `:elide-asserts` (Boolean) padrão para `false` em desenvolvimento e `true` em `release`
- `:pretty-print` e `:pseudo-names` padrão para `false`. É possível usar `shadow-cljs release app --debug` para habilitar ambos temporáriamente sem tocar no seu config. É bastante útil quando rodando em problemas com `release` builds
- `:source-map` (Boolean) padrão para `true` durante desenvolvimento, `false` para `release`.
- `:source-map-include-sources-content` (Boolean) padrão para `true` e decide se o source maps deveria conter seus sources no arquivo `.map` diretamente.
- `:source-map-detail-level` `:all` ou `:symbols` (`:symbols` no geral reduz o tamanho mas é menos preciso)
- `:externs` vetor de paths, padrão para `[]`
- `:checked-arrays` (Boolean), padrão para `false`
- `:anon-fn-naming-policy`
- `:rename-prefix` e `:rename-prefix-namespace`
- `:warnings` como um map de `{warning-type true|false}`, ex. `:warnings {:undeclared-var false}` to desligar alertas em especifico.

*Opções não suportadas*

- `:verbose` é controlado rodando `shadow-cljs compile app -versobe` não no build de configuração.
- `:foreign-libs` e `:libs`
- `:stable-names` sempre habilitados, não pode ser disabilitado
- `:install-deps`
- `:source-map-path`, `:source-asset-path` e `:source-map-timestamp`
- `:cache-analysis` sempre habilitado.
- `:recompile-dependents`
- `:preamble`
- `:hashbang` (apeneas o target `:node-script` suporta isso)
- `:compiler-stats` usa `--verbose` para capturar informações detalhadas ao invés
- `:optimize-constants` sempre para builds de release, não pode ser desabilitado
- `:parallel-build` sempre habilitado
- `:aot-cache`
- `:package-json-resolution` see <<js-resolve, :js-options :resolve>> instead
- `:watch-fn`
- `:process-shim`

=== Alertas como errors [[warnigs-as-errors]]

É aqueel momento que indesejavelmente a build falha com alertas ao invés de continuar o build (ex.: nos evento do CI). Você pode usar a opção do compilador `:warnings-as-errors` para customizar como isso é manipulado.

.Tratar todos os alertas como erro
```
{...
 :builds
 {:app
  {...
   :compiler-options {:warnings-as-errors true}}}}
```

.Capturar apenas determinados alertas
```
{...
 :builds
 {:app
  {...
   :compiler-options {:warnings-as-errors #{:undeclared-var}}}}
```

Um conjunto de possíveis keywords warning-type pode ser encontradas em https://github.com/clojure/clojurescript/blob/5ad96a8b3ae2e3616a19715ba9ba2471a36933a2/src/main/clojure/cljs/analyzer.cljc#L124-L163[here].

.Lançar apenas para determinados namespaces
```
{...
 :builds
 {:app
  {...
   :compiler-options {:warnings-as-errors {:ignore #{some.ns some.library.*}
                                           :warnings-types #{:undeclared-var}}}
```

`:ignore` aceita um conjunto de símbolos referente a namespaces. Ao invés corresponder diretamente ou `.*` são permitidos. `:warning-type` tem a mesma funcionalidade como acima. não especificando que isso significando que todos os alertas vão disparar exceto os namespaces ignorados. 

== Output Language Options

Por padrão a saída do output gerado é compatível com ES5 e todas as "novas" funcionalidade serão transpiladas usando polyfills. Isso é atualmente o padrão mais seguro e suportado pela maioria dos navegadores (incluindo IE10+).

É possível selecionar outras opções de saída se você apenas se preocupa com ambiente mais mordernos e quer manter o código original sem substituições (ex.: `node`, Extensões Chrome, ...)

IMPORTANT: Perceba que na maioria das vezes afeta código JS importando do <<npm, npm>> ou arquivos `.js` a partir do <<classpath-js, classpatch>>. CLJS vai gerar apenas saída ES5 e isso não é afetado por configurações em camadas mais altas.

É possível configurar isso através do `:output-feature-set` no `:compiler-options`. A opção antiga `:language-out` não deveria ser usada como `:output-feature-set`.

Opções suportadas

- `:es3`
- `:es5`
- `:es6` - `class`, `const`, `let`, ...
- `:es7` - operador `**`
- `:es8` - `async/await`, `generators`, ...
- `:es-next` - todas as funcionalidades que o Closure Compiler suporta atualmente

.Exemplo
```
{...
 :builds
 {:script
  {:target :node-script
   :main foo.bar/main
   ...
   :compiler-options {:output-feature-set :es7}}}}
```

A documentação dessas opções está um pouco espalhada e é melhor documentada no código https://github.com/google/closure-compiler/blob/master/src/com/google/javascript/jscomp/parsing/parser/FeatureSet.java[here].


== Leitura condicional

ATENÇÃo: Essa funcionalidade funciona apenas em `shadow-cljs`. Foi oficialmente rejeitada pelo ClojureScript https://dev.clojure.org/jira/browse/CLJS-2396[rejected]. Ainda vai compilar bem em CLJS mas funciona apenas na branch oficial. Ainda deve ser https://groups.google.com/d/msg/clojure-dev/8YJJM8lJuQs/hR5_vUZPCQAJ[suportado] um dia ainda não é

`shadow-cljs` permite configurar um leitor de funcionalidade adicional no arquivo `.cljc`. Por padrão você pode apenas usar leitor condicional para gerar código separado para `:clj`, `:cljs` ou `:cljr`. Contudo em diversos builds CLJS também é desejável selecionar qual código é gerado baseado no seu `:target`. 

Exemplo: Algum pacote `npm` apenas funciona quando apontando para `:browser`, mas você deve ter um `ns` que também quer usar no build do `:node-script`. Isso deve acontecer com frequência quando tentando usar Server-Side Rendering (SSR) com seu React App. `codemirror` é um desses pacotes. 

```clojure
(ns my.awesome.component
  (:require
    ["react" :as react]
    ["codemirror" :as CodeMirror]))

;; suppose you create a CodeMirror instance on some React :ref
(defn init-cm [dom-node]
  (let [cm (CodeMirror/fromTextArea dom-node #js {...})]
    ...))

...
```

Esse namespace vai compilar bem para ambos os builds (`:node-script` e `:browser`) mas quando tento rodar o `:node-scriot` isso vai falhar desde que o pacote `codemirror` tenta acessar o DOM. Desde `react-dow/server` não use resf `init-cm` a função nunca será chamada.

Enquando usar <<closure-defines, :closure-defines>> para condicionalmente compilar longe a fn `init-cm` você não pode usar isso para livrar-se do `:require` extra. Leitores condicionais permite vocie fazer isso facilmente.

```clojure
(ns my.awesome.component
 (:require
   ["react" :as react]
   ;; NOTE: The order here matters. Only the first applicable
   ;; branch is used. If :cljs is used first it will still be
   ;; taken by the :server build
   #?@(:node [[]]
       :cljs [["codemirror" :as CodeMirror]])))

#?(:node ;; node platform override
   (defn init-cm [dom-node]
    :no-op)
   :cljs ;; default impl
   (defn init-cm [dom-node]
     ... actual impl ...))

...
```

.`:reader-features` exemplo de configuração
```clojure
{...
 :builds
 ;; app build configured normally, no adjustments required
 {:app
  {:target :browser
   ...}
  ;; for the server we add the :node reader feature
  ;; it will then be used instead of the default :cljs
  :server
  {:target :node-script
   :compiler-options
   {:reader-features #{:node}}}}}
```

O build `:server` então ira não mais tão longe tendo o `codemirror` requerido e a função `init-cm` é removida. Se tortando apenas

```clojure
(ns my.awesome.component
  (:require
    ["react" :as react]))

;; this will likely be removed as dead code if
;; its never actually called anywhere

(defn init-cm [dom-node] :no-op)
...
```
IMPORTANT: Essa funcionalidade é apenas disponível em arquivos `.cljc` e irá falhar em arquivos `.cljs`.

== Overriding from the CLI [[config-merge]]

It is sometimes desirable to make small adjustments to the build configuration from the command line with values that can't be added statically to the `shadow-cljs.edn` config or may change depending on the environment you are in.

You can pass additional config data via the `--config-merge {:some "data"}` command line option which will be merged into the build config. Data added from the CLI will override data from the `shadow-cljs.edn` file.

.Example `shadow-cljs.edn` config
```
{...
 :builds
 {:app
  {:target :browser
   :output-dir "public/js"
   ...}}}
```

.Overriding the `:output-dir` from the CLI
```bash
$ shadow-cljs release app --config-merge '{:output-dir "somewhere/else"}'
```

.Overriding the `:closure-defines` from the CLI
```bash
$ shadow-cljs release app --config-merge '{:closure-defines {your.app/DEBUG true}}'
```

`--config-merge` expects one EDN map and can be used multiple times, they will be merged left to right. The data added is also visible to build-hooks.

IMPORTANT: If you specify multiple build ids the data will be merged into all specified builds. `shadow-cljs release frontend backend --config-merge '{:hello "world"}'` will be applied to both.

== Using Environment Variables [[shadow-env]]

It is possible to use environment variables to set configuration values in `shadow-cljs.edn` but you should consider using `--config-merge` instead. If you really must use an environment variable you can do so via the `#shadow/env "FOO"` reader tag.

.Example `shadow-cljs.edn` config
```
{...
 :builds
 {:app
  {:target :browser
   :output-dir "public/js"
   :closure-defines {your.app/URL #shadow/env "APP_URL"}
   ...}}}
```

IMPORTANT: The environment variables used when the `shadow-cljs` process was started are used. If a server process is used its environment variables will be used over those potentially set by other commands. This is mostly relevant during development but may be confusing. `--config-merge` does not have this limitation.

== Build and Target defaults [[build-target-defaults]]

It is possible to use set defaults that will be used for all builds, or for all targets of a certain type.

Configuration merge order is as follows `:build-defaults` -> `:target-defaults` -> actual build config -> extra config overrides.

.Example `shadow-cljs.edn` config
```
{...
 :build-defaults
 {:closure-defines 
   {your.app/VERBOSE true}}
   
 :target-defaults
 {:browser
   {:js-options
     {:resolve {"react" {:target :global
                         :global "React"}}}}}
 
 :builds
 {:app
  {:target :browser
   ...}}}
```

In this example the `:app` target will inherit both `:build-defaults` and the `:target-defaults` for `:browser`.

IMPORTANT: Configs later in the merge order can override, but not remove previous configuration items. Once a default is set, the only way to remove it is by overriding it.
