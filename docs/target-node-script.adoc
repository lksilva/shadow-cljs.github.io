O `:target :node-script` produz uma única saída de arquivo padrão que pode ser rodada usando `node.js`.
O código é apenas ClojureScript, e um ponto de entrada é fácil de definir:

```
(ns demo.script)

(defn main [& cli-args]
  (prn "hello world"))
```

=== Opções de Build

Você vai precisar apenas de algumas configurações básicas <<config,main configuration>> como em outros targets (como `:source-paths`), mas você vai precisar construir algumas opções especificas do node:

[horizontal]
`:main`:: (obrigatório). O namespace qualificado para ser o ponto de entrada do seu script.
`:output-to`:: (required). O caminho e o nome do arquivo para o script gerado.
`:output-dir`:: (optional). O caminho para suportar arquivos em modo de desenvolvimento. O padrão para um diretório de cache.

// TODO: Thomas mentioned that node can be picky and sometimes output-dir is useful...an example would be nice.

.Exemplo de um node script build
```
{:source-paths [...]
 ...
 :builds
 {:script
  {:target :node-script
   :main demo.script/main
   :output-to "out/demo-script/script.js"}}}
```

Quando compilado isso resulta em um arquivo padrão `out/demo-script/script.js` pretendido ser chamado através do
`node script.js <command line args>`. Quando rodando isso vai chamar a função `(demo.script/main <command line args>)`
na inicialização. Isso sempre produz arquivos especificados em `:output-to`. Qualquer outros arquivos de suporte
(ex.: para modo de desenvolvimento) são escritos e um diretório temporário de suporte.

=== Hot Code Reload [[NodeHotCodeReload]]

Você vai frequentemente escrever scripts que rodam como um servidor ou algum outro processo de longa duração. Hot code reload pode ser
bastante útil quando trabalhando isso, e é bem simples de configurar:

. E start/stop funções de callback.
. Configure o build para usar esses hooks.

Aqui está um exemplo de um servidor http em node:

.Exemplo de um script em node com hooks de start/stop para hot reload do código
```
(ns demo.script
  (:require ["http" :as http]))

(defn request-handler [req res]
  (.end res "foo"))

; a place to hang onto the server so we can stop/start it
(defonce server-ref
  (volatile! nil))

(defn main [& args]
  (js/console.log "starting server")
  (let [server (http/createServer #(request-handler %1 %2))]

    (.listen server 3000
      (fn [err]
        (if err
          (js/console.error "server start failed")
          (js/console.info "http server running"))
        ))

    (vreset! server-ref server)))

(defn start
  "Hook to start. Also used as a hook for hot code reload."
  []
  (js/console.warn "start called")
  (main))

(defn stop
  "Hot code reload hook to shut down resources so hot code reload can work"
  [done]
  (js/console.warn "stop called")
  (when-some [srv @server-ref]
    (.close srv
      (fn [err]
        (js/console.log "stop completed" err)
        (done)))))

(js/console.log "__filename" js/__filename)
```

A configuração associada é (`shadow-cljs.edn`):

.Adicionando hooks para hot reload do código.
```
{...
 :builds
   { :script {... as before

              ; add in reload hooks
              :devtools {:before-load-async demo.script/stop
                         :after-load demo.script/start}}}}
```

ALERTA: Muitas bibliotecas escodem estado ou fazem ações que dificultam funcionamento do hot reloading do código. 
Não há nada que o compilado possa fazer para melhorar isso desde ele não tem conhecimento do que essas bibliotecas estão fazendo.
Hot reload do código vai funcionar bem em situações onde você consegue "stop" e "restart" os artefatos usados.
